/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   Mux16(a=x, b[0..15]=false, sel=zx, out=out1);

   Mux16(a=out1, b=notx, sel=nx, out=out2);
   Not16(in=out1, out=notx);

   Mux16(a=y, b[0..15]=false, sel=zy, out=out3);

   Mux16(a=out3, b=noty, sel=ny, out=out4);
   Not16(in=out3, out=noty);

   Mux16(a=out6, b=out5, sel=f, out=out7);
   Add16(a=out2, b=out4, out=out5);
   And16(a=out2, b=out4, out=out6);

   Mux16(a=out7, b=notf, sel=no, out[15]=ng, out[0..7]=out11, out[8..15]=out22, out=out);
   Not16(in=out7, out=notf);

   Or8Way(in=out11, out=orout);
   Or8Way(in=out22, out=orout2);

   Or(a=orout, b=orout2, out=orout3);
   Not(in=orout3, out=zr);
}